Use DFClient.pkg
Use cRegistrationsDataDictionary.dd
Use DFEntry.pkg
Use File_dlg.pkg
Use cApplication.pkg
Use GlobalFunctionsProcedures.pkg
Use seq_chnl.pkg
Use cIniFile.pkg
Use Windows.pkg

Object oCreateIniFile is a dbModalPanel
    Set Label to "CreateIniFile"
    Set Size to 218 378
    Set piMinSize to 89 211
    Set Location to 12 0
    Set Border_Style to Border_Thick

    Property Integer piID
    
    Object oRegistrations_DD is a cRegistrationsDataDictionary
    End_Object

    Set Main_DD to oRegistrations_DD
    Set Server to oRegistrations_DD

    Object oOK_btn is a Button
        Set Label    to "Create"
        Set Location to 198 266
        Set peAnchors to anBottomRight
        Set Default_State to True

        Procedure OnClick
            Send CreateRegIniFile   
        End_Procedure 
        
        Procedure CreateRegIniFile
            String sPath sFileName sHeader sLicenseType sSerialNo sRegName sRegCode sMsg
            Integer iRetval
            Handle hoIni hoDD      
            Boolean bSavePath bOK
            
            Move (Main_DD(Self)) to hoDD
            Get Field_Current_Value of hoDD Field Registrations.SN   to sSerialNo
            Get Field_Current_Value of hoDD Field Registrations.NAME to sRegName
            Get Field_Current_Value of hoDD Field Registrations.CODE to sRegCode 
            Get Field_Current_Value of hoDD Field Registrations.Product_Class to sLicenseType 
            If (sSerialNo = "0") Begin
                Send Info_Box "You need to find a Registration record first"
                Procedure_Return
            End
            
            Get Value of oIniFileName_fm to sFileName
            If (sFileName = "") Begin
                Send Info_Box "You need to specify an ini-file name first"
                Procedure_Return
            End
            Get YesNo_Box "Do you want to create an ini-file for the currently selected data now?\nThe ini-file can later be used by the DataFlex Registration program to easily import the license data." to iRetval
            If (iRetval <> MBR_Yes) Begin
                Procedure_Return
            End

            If (sLicenseType = "DataFlex") Begin
                Move "DataFlex_License_Information" to sHeader
            End 
            Else Begin
                Move "DataFlex_Reports_License_Information" to sHeader
            End
            
            Get Checked_State of oSavePath_cb to bSavePath
            Get SavePath bSavePath sFileName to bOK
                
            Move False to Err
            Get Create (RefClass(cIniFile)) to hoIni
            Set psFileName   of hoIni to sFileName
            Send WriteString of hoIni sHeader "Serial_Number"     sSerialNo
            Send WriteString of hoIni sHeader "Registration_Name" sRegName
            Send WriteString of hoIni sHeader "Registration_Code" sRegCode
            Send Destroy     of hoIni
            If (Err = True) Begin
                Move ("Sorry, the ini-file:\n" + sFileName + "\ncould not be created properly.") to sMsg
            End                                                                                         
            Else Begin
                Move "Success! The ini-file has been created." to sMsg
            End
            Send Info_Box sMsg
        End_Procedure

    End_Object

    Object oCancel_btn is a Button
        Set Size to 14 50
        Set Label    to "&Close"
        Set Location to 198 320
        Set peAnchors to anBottomRight

        Procedure OnClick
           Send Close_Panel
        End_Procedure

    End_Object

    Object oIniFileName_fm is a Form
        Set Size to 12 247
        Set Location to 143 68
        Set Label_Justification_Mode to JMode_Top
        Set Label_Col_Offset to 0
        Set Label to "Registration Ini-File name:"
        Set peAnchors to anTopLeftRight
    End_Object

    Object oSelect_btn is a Button
        Set Size to 12 50
        Set Location to 143 320
        Set Label to "Select"
        Set peAnchors to anTopRight
    
        Procedure OnClick
            Handle ho
            String sPath sFileName 
            Boolean bOpen
            
            Get Value of oIniFileName_fm to sFileName
            Get ExtractFilePath sFileName to sPath 
            Get Create (RefClass(OpenDialog)) to ho
            Set Filter_String       of ho to "Ini-Files|*.ini|All Files|*.*"
            Set Initial_Folder      of ho to sPath  
            Set MultiSelect_State   of ho to False
            
            Get Show_Dialog of ho to bOpen
            If (bOpen = True) Begin
                Get File_Name of ho to sFileName
                Set Value of oIniFileName_fm to sFileName    
            End
            Send Destroy of ho
        End_Procedure
    
    End_Object                          

    Object oSavePath_cb is a CheckBox
        Set Size to 10 50
        Set Location to 161 68
        Set Label to "Save Path"
    End_Object

    Object oInfo2_tb is a TextBox
        Set Auto_Size_State to False
        Set Size to 17 264
        Set Location to 174 68
        Set Label to "Note: A registration code for a more a recent DataFlex version also works for older versions."
        Set Justification_Mode to JMode_Left
        Set FontWeight to fw_Bold
    End_Object

    Object oDataInfo_grp is a dbGroup
        Set Size to 114 357
        Set Location to 6 11
        Set Label to "Data Record Info"

        Object oInfo1_tb is a TextBox
            Set Auto_Size_State to False
            Set Size to 31 187
            Set Location to 12 153
            Set Label to "Note: Ini-files can be imported from the DataFlex License Registration program. This makes it easier than manually typing the license information."
            Set Justification_Mode to JMode_Left
        End_Object
        Object oRegistrations_ID is a dbForm
            Entry_Item Registrations.ID
            Set Location to 21 55
            Set Size to 12 54
            Set Label to "ID:"
            Set Label_Justification_mode to jMode_right
            Set Label_Col_Offset to 2
            Set Label_row_Offset to 0
            Set Enabled_State to False
        End_Object
        Object oRegistrations_SN is a dbForm
            Entry_Item Registrations.SN
            Set Location to 35 55
            Set Size to 12 78
            Set Label to "SN:"
            Set Label_Justification_mode to jMode_right
            Set Label_Col_Offset to 2
            Set Label_row_Offset to 0
            Set Enabled_State to False
        End_Object
        Object oRegistrations_NAME is a dbForm
            Entry_Item Registrations.NAME
            Set Location to 49 55
            Set Size to 12 200
            Set Label to "NAME:"
            Set Label_Justification_mode to jMode_right
            Set Label_Col_Offset to 2
            Set Label_row_Offset to 0
            Set Enabled_State to False
        End_Object
        Object oRegistrations_CODE is a dbForm
            Entry_Item Registrations.CODE
            Set Location to 63 55
            Set Size to 12 200
            Set Label to "CODE:"
            Set Label_Justification_mode to jMode_right
            Set Label_Col_Offset to 2
            Set Label_row_Offset to 0
            Set Enabled_State to False
        End_Object
        Object oRegistrationsProduct_Class is a dbComboForm
            Entry_Item Registrations.Product_Class
            Set Size to 12 199
            Set Location to 76 55
            Set peAnchors to anTopLeftRight
            Set Label to "Product Class"
            Set Label_Justification_mode to jMode_right
            Set Label_Col_Offset to 2
            Set Label_row_Offset to 0
            Set Entry_State to False
            Set Enabled_State to False
        End_Object 
        Object oRegistrationsProduct_Name is a dbForm
            Entry_Item Registrations.Product_Name
            Set Size to 12 250
            Set Location to 90 55
            Set peAnchors to anTopLeftRight
            Set Label to "Product Name"
            Set Label_Justification_mode to jMode_right
            Set Label_Col_Offset to 2
            Set Label_row_Offset to 0
            Set Enabled_State to False
        End_Object 
    End_Object
    
    Function SavePath Boolean bSavePath String sFullPath Returns Boolean
        Handle hoIni     
        String sPath sFileName sProgramPath
        Integer iErr
        Boolean bRetval
        
        Move Err to iErr
        Get psProgramPath of (phoWorkspace(ghoApplication)) to sProgramPath
        Move (sProgramPath + "\Config.ini") to sFileName
        
        Get ExtractFilePath sFullPath to sPath
        Get Create (RefClass(cIniFile)) to hoIni
        Set psFileName   of hoIni to sFileName
        Send WriteString of hoIni "Save Path" "bSavePath" (If(bSavePath = True, "T", "F"))
        Send WriteString of hoIni "Save Path" "Path" sPath
        Send Destroy     of hoIni
        
        Move (Err = iErr) to bRetval
        Move iErr to Err
        Function_Return bRetval
    End_Function

    Function ReadPath Returns String
        Handle hoIni     
        String sPath sFileName sProgramPath sState
        
        Move "" to sPath
        Get psProgramPath of (phoWorkspace(ghoApplication)) to sProgramPath
        Move (sProgramPath + "\Config.ini") to sFileName
        
        Get Create (RefClass(cIniFile)) to hoIni
        Set psFileName  of hoIni to sFileName
        Get ReadString  of hoIni "Save Path" "bSavePath" "T" to sState
        If (sState = "T") Begin
            Get ReadString  of hoIni "Save Path" "Path"      ""  to sPath
        End
        Send Destroy of hoIni
        
        Function_Return sPath
    End_Function

    Procedure Page Integer iPageObject
        Forward Send Page iPageObject
        Set Icon to "Default.ico"    
    End_Procedure
    
    Procedure Popup       
        String sPath sFileName sDFVersion sLicenseType sHeader
        Integer iVersion iRevision iBuild iID
        Handle hoDD
        
        Move (Main_DD(Self)) to hoDD
        Send Clear_All of hoDD
        Get piId to iID
        Move iID to Registrations.ID
        Find eq Registrations.ID                       
        Send Request_Assign of hoDD
        Move (Trim(Registrations.Product_Class)) to sLicenseType

        If (sLicenseType = "DataFlex") Begin
            Move "DataFlex_License_Information" to sHeader
            Move "DFRegistrationIniFile"        to sFileName
        End 
        Else Begin
            Move "DataFlex_Reports_License_Information" to sHeader
            Move "DFReportsRegistrationIniFile"         to sFileName
        End
        
        Get ReadPath to sPath
        If (sPath = "") Begin
            Get psHome of (phoWorkspace(ghoApplication)) to sPath
        End
        Else Begin
            Set Checked_State of oSavePath_cb to True
        End                                          
        
        Version_Information iVersion iRevision iBuild
        Append sFileName (String(iVersion) + "." + String(iRevision) + ".ini")
        Move (Trim(sPath) + sFileName) to sFileName
        Set Value of oIniFileName_fm to sFileName
        
        Forward Send Popup    
    End_Procedure

    On_Key Key_Alt+Key_O Send KeyAction of oOk_btn
    On_Key Key_Alt+Key_C Send KeyAction of oCancel_btn
    On_Key kCancel       Send Close_Panel
End_Object

Procedure Initialize_CreateIniFileDialog Integer iID
    Handle ho
    
    Move (oCreateIniFile(Self)) to ho
    
    Set piID of ho to iID
    Send Popup of ho
End_Procedure